pkgname=mate-desktop
pkgver=1.1.0
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=('libunique' 'libmatenotify' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'mate-conf')
makedepends=('mate-doc-utils' 'intltool')
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects/mate/"
groups=('mate')
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('33640476894500a291bc60c421028e31a3a952f26ea08f35fe989fc8355ba590')

build() {
    cd "$srcdir/$pkgname"

    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-scrollkeeper \
        --disable-startup-notification \
        --enable-unique || return 1
    # --disable-nyancat
    make || return 1
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install

    # prevent conflicts errors
    mv "${pkgdir}/usr/share/omf" "${pkgdir}/usr/share/mate-omf"
}
